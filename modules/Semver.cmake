include_guard(GLOBAL)

include(CPM)
cpm()

if(NOT DEFINED SEMVER_REPOSITORY)
  set(SEMVER_REPOSITORY "https://github.com/Neargye/semver.git")
endif()

if(NOT DEFINED SEMVER_TAG)
  set(SEMVER_TAG "v0.3.0")
endif()

declare_option(REPOSITORY Semver OPTION SEMVER_OPT_BUILD_EXAMPLES VALUE OFF)
declare_option(REPOSITORY Semver OPTION SEMVER_OPT_BUILD_TESTS VALUE OFF)
declare_option(REPOSITORY Semver OPTION SEMVER_OPT_INSTALL VALUE ON)
print_options(REPOSITORY  Semver)

CPMAddPackage(NAME Semver
    GIT_REPOSITORY ${SEMVER_REPOSITORY}
    GIT_TAG ${SEMVER_TAG}
    FETCHCONTENT_UPDATES_DISCONNECTED ${IS_OFFLINE}
    OPTIONS "${Semver_OPTIONS}")

#configure file with Semver parameters
function(semverfile)
  cmake_parse_arguments(ARG "" "MAJOR;MINOR;PATCH;PRERELEASE;PRERELEASE_NUMBER;INPUT;OUTPUT" "" "${ARGN}")
  if(DEFINED SEMVER_PRERELEASE)
    if(SEMVER_PRERELEASE STREQUAL "alpha" OR SEMVER_PRERELEASE STREQUAL "beta" OR SEMVER_PRERELEASE STREQUAL "rc")
      string(TOUPPER ${SEMVER_PRERELEASE} ARG_PRERELEASE)
    else()
      missive(FATAL_ERROR "SEMVER_PRERELEASE must be alpha, beta or rc !")
    endif()
  endif()

  if(NOT DEFINED ARG_INPUT)
    missive(FATAL_ERROR "INPUT not defined")
  endif()
  if(NOT DEFINED ARG_OUTPUT)
    missive(FATAL_ERROR "OUTPUT not defined")
  endif()

  if(NOT DEFINED ARG_MAJOR)
    if(PROJECT_VERSION_MAJOR STREQUAL "")
      set(SEMVER_MAJOR "0")
    else()
      set(SEMVER_MAJOR "${PROJECT_VERSION_MAJOR}")
    endif()
  else()
    set(SEMVER_MAJOR "${ARG_MAJOR}")
  endif()
  if(NOT DEFINED ARG_MINOR)
    if(PROJECT_VERSION_MINOR STREQUAL "")
      set(SEMVER_MINOR "0")
    else()
      set(SEMVER_MINOR "${PROJECT_VERSION_MINOR}")
    endif()
  else()
    set(SEMVER_MINOR "${ARG_MINOR}")
  endif()
  if(NOT DEFINED ARG_PATCH)
    if(PROJECT_VERSION_PATCH STREQUAL "")
      set(SEMVER_PATCH "0")
    else()
      set(SEMVER_PATCH "${PROJECT_VERSION_PATCH}")
    endif()
  else()
    set(SEMVER_PATCH "${ARG_PATCH}")
  endif()
  if(DEFINED SEMVER_PRERELEASE_NUMBER)
    set(ARG_PRERELEASE_NUMBER ${SEMVER_PRERELEASE_NUMBER})
  endif()
  if(NOT DEFINED ARG_PRERELEASE_NUMBER)
    set(SEMVER_PRERELEASE_NUMBER "0")
  else()
    set(SEMVER_PRERELEASE_NUMBER "${ARG_PRERELEASE_NUMBER}")
  endif()

  if(NOT DEFINED ARG_PRERELEASE)
    set(SEMVER_PRERELEASE "semver::prerelease::none")
    set(SEMVER_PRERELEASE_STRING "")
  elseif(ARG_PRERELEASE STREQUAL "ALPHA")
    set(SEMVER_PRERELEASE "semver::prerelease::alpha")
    set(SEMVER_PRERELEASE_STRING "alpha")
  elseif(ARG_PRERELEASE STREQUAL "BETA")
    set(SEMVER_PRERELEASE "semver::prerelease::beta")
    set(SEMVER_PRERELEASE_STRING "beta")
  elseif(ARG_PRERELEASE STREQUAL "RC")
    set(SEMVER_PRERELEASE "semver::prerelease::beta")
    set(SEMVER_PRERELEASE_STRING "rc")
  endif()

  configure_file(${ARG_INPUT} ${ARG_OUTPUT} @ONLY)
endfunction()
